apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    buildToolsVersion '28.0.3'

    def versionPropsFile = file('version.properties')
    def versionBuild

    if (versionPropsFile.canRead()) {
        Properties versionProps = new Properties()

        versionProps.load(new FileInputStream(versionPropsFile))

        def code = versionProps['VERSION_CODE'].toInteger() + 1

        versionBuild = code.toString()

        versionProps['VERSION_CODE'] = versionBuild
        versionProps.store(versionPropsFile.newWriter(), null)
    } else {
        throw new GradleException("Could not read version.properties!")
    }

    defaultConfig {
        applicationId "frc3824.rscout2018"
        minSdkVersion 23 // Needs to stay for the Nexus 7
        targetSdkVersion 26
        versionCode 1
        versionName "3.0.0." + versionBuild
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:3.0.1'
    }
    dataBinding {
        enabled = true
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:26.1.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.android.support:support-v4:26.1.0'
    compile 'com.android.support:design:26.1.0'
    compile 'com.android.support:support-v13:26.1.0'
    compile 'com.github.jd-alexander:android-flat-button:v1.1'
    compile 'com.marcinmoskala.activitystarter:activitystarter:1.00'
    annotationProcessor 'com.github.marcinmoskala.activitystarter:activitystarter-compiler:1.00'
    compile 'com.github.PhilJay:MPAndroidChart:v2.2.5'
    compile 'com.sdsmdg.tastytoast:tastytoast:0.1.1'
    compile 'com.github.bumptech.glide:glide:4.1.1'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.1.1'
    //compile 'com.flurgle:camerakit:0.9.17'
    compile 'com.synnapps:carouselview:0.1.4'
    compile 'com.ogaclejapan.smarttablayout:library:1.6.1@aar'
    compile 'com.ogaclejapan.smarttablayout:utils-v13:1.6.1@aar'
    compile 'com.squareup.okhttp3:okhttp:3.9.1'
    compile 'com.google.code.gson:gson:2.8.2'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    // Because RxAndroid releases are few and far between, it is recommended you also
    // explicitly depend on RxJava's latest version for bug fixes and new features.
    // (see https://github.com/ReactiveX/RxJava/releases for latest 2.x.x version)
    implementation 'io.reactivex.rxjava2:rxjava:2.1.9'
    compile 'com.google.firebase:firebase-core:11.8.0'
    compile 'com.google.firebase:firebase-database:11.8.0'
    testCompile 'junit:junit:4.12'
    implementation 'com.android.support:support-annotations:28.0.0-alpha1'
}
apply plugin: 'com.google.gms.google-services'